@printer Wide
@bgm Wind fade:1
@spawn SunShafts params:0.5
@back Desert time:1
@wait 1
@char n pos:75,-100 time:0
@hide n time:0
@sfx Paper volume:0.5
@char n pos:75,-85 wait!
@wait 1.5
@sfx FabricRightToLeft
@wait 0.15
@char n look:right pos:25,-75 wait!
@wait 0.35
@sfx Paper
@spawn ShakeCharacter params:n,1,0.3,0,0.5,0 wait!
@wait 0.5
@sfx Paper
@char n look:left pos:35,-80 wait!
@stopSfx Paper
...
@hidePrinter
@sfx FabricLeftToRight
@wait 0.15
@char n look:right pos:85,-55 wait!
@wait 0.5
@sfx Paper volume:0.5
@char n look:left pos:85,-45 wait!
@sfx Paper
@char n.Surprise wait!
@spawn ShakeCharacter params:n,1,0.15,0,0.3,0 wait!
@wait 0.5
@char n.Default pos:50,-10
@stopBgm Wind fade:5
@bgm CloudNine fade:1
@wait 0.25
n: Oh, hey there!
n: Welcome to the demo![i] I'll show what you can achieve with Naninovel along with some brief tutorials.
n: By the way, this whole demo is done entirely with Naninovel, without even a line of code.
n: Shall we get started then?
@save

@char n pos:50,-10 time:0

n: Imagine you've imported Naninovel package to your Unity project.[i][char b.CreateScript transition:Ripple time:1 pos:{pos}][char n pos:80 time:0.5] For starters, create a <b>naninovel script</b>.[i] Naninovel scripts are text documents where you control what happens on scenes.[i] Open them with a text editor of your choice, like Notepad, Word or Atom.

@char b.OpenScript transition:Ripple time:1
n: For example, here is the contents of a naninovel script used for this demo so far.[i] Don't worry if it looks complicated, I'll show how it works step by step a bit later.

@hide b
n: Now, open the script in a text editor.[i][char b.HelloWorld transition:Ripple time:1 pos:{pos}] Let's start with showing a simple text. Simply type the text you wish to show, like this.[i] Enter play mode and start new game to see the result.[< skip!]

@choice "Hello World!" goto:.Hello
@stop

# Hello
Hello World!

@hide b
n: Easy, right?[i][char b.Name transition:Ripple time:1 pos:{pos}] To associate shown text with a character, just add its name followed by a colon before the text.[< skip!]

@choice "John: Hello World!" goto:.John
@stop

# John
John: Hello World!

@hide b
n: Now, let's add a <b>character</b> on scene.[i] Characters in Naninovel can be based on regular and diced sprites, animated Live2D models and 3D meshes; you can add your own implementations as well.[i] To keep it simple, let's use a regular sprite implementation for now.

@char b.AddCharacter pos:{pos} transition:Ripple time:1
n: Using Naninovel editor menu, open characters manager and add a new character record specifying its name. Double click the record to open settings, where you can select the implementation, add appearance assets and tweak other settings.

@save
@hide b
@stopBgm fade:5
@bgm Wind fade:5
n: Alright, it's time to summon Kohaku-chan on scene![i][char b.ShowCharacter transition:Ripple time:1 pos:{pos}] Type the following expression in the naninovel script and run the game.[< skip!]

@choice "@char Kohaku" goto:.Kohaku
@stop

# Kohaku
@hide b
@hidePrinter
@wait 0.5
@char k look:right pos:25 tint:#ffffff00 time:0
@char k.Default transition:Ripple tint:#ffffffff easing:EaseOutCubic time:3 wait!
@style b
k: .[wait 0.5] .[wait 0.5] .
@hidePrinter
@char k.Confidence wait!
@char k.Default wait!
@char k.Surprise wait!
@char k look:left wait!
@wait 0.25
@char k look:right wait!
@spawn ShakePrinter params:,3
@style default
k: Whoa, what's going on?! Where am I?

@sfx Paper
@spawn ShakeCharacter params:n,1,0.3,0,0.5,0
n.Surprise: Oh dear, looks like we've snatched you away in the middle of a class.[i][char n.Default] Name's Nani-kun, sorry for the sudden call. Would you help us with this demo tutorial, please?

k.Angry: Gosh, yet another Asset Store stuff? Why they always use me to show it off?..
@char k.Default pos:35,0
k: Though...[i][char k pos:37.5] Those ears, so fluffy.[i][char k pos:40][char n pos:80 scale:0.95] I just can't resist...

@char n pos:85 scale:0.925
n: Uhm, wait...[i] What[< skip!]

@hidePrinter
@stopBgm fade:1
@bgm JellyRolls fade:1
@char k.Happy look:left pos:90,,1
@char n.Pain look:left pos:82,-10,-1 scale:0.9 wait!
@spawn ShakeCharacter params:n,2,0.3,0,0.5,0
@sfx Paper

n: No, stop! My ears...

@hidePrinter
@arrange k.15,n.25 wait!
@sfx Paper
@spawn ShakeCharacter params:n,2,0.3,0,0.5,0
n: You have to do something, use naninovel script![< skip!]

@set triedMoving=false
@set triedAngry=false
@set tries=0

# Choices
@choice "@char Kohaku.Angry" goto:.Angry if:"!triedAngry"
@choice "@char Kohaku pos:85" goto:.Move if:"!triedMoving"
@print "Hide! Use the hide command!" author:n wait! if:tries==2
@choice "@hide Kohaku" goto:.Hide if:tries==2
@stop

# Move
@set triedMoving=true
@char k.Pain look:left pos:85 wait!
n.Confidence: You've moved her away! Good job!
@char k.Default wait!
k: You won't get away from my passion!
@char k.Happy look:right pos:15
@sfx Paper
@char n.Pain
@spawn ShakeCharacter params:n,1,0.3,0,0.5,0
@set tries++
@goto .Choices

# Angry
@set triedAngry=true
@spawn Blur params:,1,0.5
@camera offset:-2.8,1.5 zoom:0.3 easing:SmoothStep time:0.5
@char k.Angry wait!
n.Surprise: You just made her angry! Not sure if it'll help...
k.Happy: No way!
@despawn Blur params:0.35
@camera offset:0,0 zoom:0
@sfx Paper
@char n.Pain
@spawn ShakeCharacter params:n,1,0.3,0,0.5,0
@set tries++
@goto .Choices

# Hide
@hide k wait!
n.Confidence: That's it! You've removed her from the scene!
@char n scale:1
@arrange wait!
@stopBgm fade:1
@bgm Wind fade:1
n: Guess we should have called someone else[sfx Glitter volume:0.25][spawn DigitalGlitch params:0.1] instead...
n.Default: Wait, did you hear[sfx Glitter volume:0.5][spawn DigitalGlitch params:0.25] that?
n.Surprise: No way...[i] She's hacking[sfx Glitter][sfx Bubbles volume:0.5][spawn DigitalGlitch] through![i] They don't call her <b>Unity</b>-chan for nothing!
@sfx Bubbles
@spawn DigitalGlitch params:2.5,1.5
@sfx Glitter
@wait 1
@sfx Glitch
@hidePrinter
@char n pos:65 scale:0.9
@char k look:right pos:25 tint:#ffffff00 time:0
@char k.Angry transition:Pixelate tint:#ffffff easing:EaseOutCubic time:2.5
@spawn Blur params:,1
@camera offset:-3.75,2 zoom:0.45 easing:SmoothStep time:1 wait!
@wait 2
@char k.Happy wait!
@wait 0.5
@despawn Blur
@camera offset:0,0 zoom:0 easing:SmoothStep time:1
@arrange
@hidePrinter
@char n scale:0.9
@stopBgm fade:1
@bgm JellyRolls fade:1
@arrange k.85,n.75 wait!
@sfx Paper
@char n.Pain

@set shakeCount=0
# ShakeLoop
@spawn ShakeCharacter params:n,2,0.3,,0.5 wait!
@char n.Surprise wait!
@char n.Pain
@spawn ShakeCharacter params:n,5,0.15,,0.25,,true,false wait!
@char n.Surprise wait!
@set shakeCount++
@goto .ShakeLoop if:shakeCount<3

k.Confidence: That's what I call happiness overdose!
@char n.Default scale:1
@char k.Default
@arrange wait!
n.Pain: I was going to die...
@hidePrinter
@wait 1

n.Default: By the way, those glitch effects were scary as hell; was it some kind of an <b>@spawn</b> command?
k.Confidence: Yep, similar to the one you've used for shaking text window.[i] Wanna try?[< skip!]

# FXChoice
@choice "@spawn DigitalGlitch" goto:.Glitch
@choice "@spawn ShakePrinter" goto:.Shake
@choice "@spawn Rain" goto:.Rain
@choice "@spawn Snow" goto:.Snow
@choice "@spawn Blur" goto:.Blur
@choice "That's enough" goto:.Enough

@stop

# Glitch
@spawn DigitalGlitch wait!
@goto .FXChoice

# Shake
@spawn ShakePrinter wait!
@goto .FXChoice

# Rain
@spawn Rain wait!
@goto .FXChoice

# Snow
@spawn Snow wait!
@goto .FXChoice

# Blur
@spawn Blur params:,0.75 wait!
@goto .FXChoice

# Enough
@despawnAll
n: And of course you can add your own custom effects. It's as easy as dropping a prefab with the effect to spawn resources list; then use <b>@spawn</b> command followed by the prefab name to activate it.
n: It's also possible to place a command inside the text, so that it could be executed at the right moment. Like this.[< skip!]

@choice "John: Shake\[spawn ShakePrinter\] it!" goto:.JohnShake
@stop
# JohnShake
John: Shake[spawn ShakePrinter wait!] it!

@stopBgm fade:10
@bgm Wind fade:10
k.Default: By the way, are there other ways to output the text? Like, if I want to print a whole paragraph â€” it won't even fit this window.
n: Actually, the window can resize to fit as mush as the screen allow, but there is another way.[i] You can use a <b>fullscreen</b> printer; it's specifically designed for cases like this.
n: Use <b>@printer</b> command to switch printers. There are several built-in ones, and you can always add your own. Try it out![< skip!]

# PrinterChoice
@choice "@printer Fullscreen" goto:.FullscreenPrinter
@choice "@printer Dialogue" goto:.DialoguePrinter
@choice "@printer Wide" goto:.WidePrinter
@choice "@printer Bubble" goto:.BubblePrinter
@choice "Ok, I've got it!" goto:.EndPrinter
@stop

# FullscreenPrinter
@hideChars
@printer Fullscreen
@resetText
Lorem ipsum dolor sit amet, consectetur adipiscing elit. Nam porta, turpis id porttitor maximus, mi tellus tempus justo, sit amet porttitor lorem velit vitae libero. Nunc dapibus maximus molestie. Etiam luctus arcu in lorem tristique tristique. Vestibulum tincidunt eget ante eget sodales.[i][br][br]Proin convallis purus mi, sit amet egestas nibh suscipit vitae. Praesent tempor lectus ex, at vehicula arcu condimentum vel. Vestibulum eu fermentum odio, eleifend euismod nibh. Pellentesque habitant morbi tristique senectus et netus et malesuada fames ac turpis egestas.[i][br][br]Duis eu pellentesque magna, ut molestie eros. Integer iaculis accumsan nisl, vel rutrum enim bibendum non. Sed dui ipsum, sodales ut rutrum ac, elementum ac lectus.
@hidePrinter
@goto .PrinterChoice

# DialoguePrinter
@hideChars
@printer Dialogue
Lorem ipsum dolor sit amet, consectetur adipiscing elit. Nam porta, turpis id porttitor maximus, mi tellus tempus justo, sit amet porttitor lorem velit vitae libero. Vestibulum tincidunt eget ante eget sodales.[i][br][br]Proin convallis purus mi, sit amet egestas nibh suscipit vitae. Praesent tempor lectus ex, at vehicula arcu condimentum vel. Vestibulum eu fermentum odio, eleifend euismod nibh. Pellentesque habitant morbi tristique senectus et netus et malesuada fames ac turpis egestas.[i][br][br]Duis eu pellentesque magna, ut molestie eros. Integer iaculis accumsan nisl, vel rutrum enim bibendum non. Sed dui ipsum, sodales ut rutrum ac, elementum ac lectus.
@hidePrinter
@goto .PrinterChoice

# WidePrinter
@hideChars
@printer Wide
Lorem ipsum dolor sit amet, consectetur adipiscing elit. Nam porta, turpis id porttitor maximus, mi tellus tempus justo, sit amet porttitor lorem velit vitae libero. Vestibulum tincidunt eget ante eget sodales.
@hidePrinter
@goto .PrinterChoice

# BubblePrinter
@hideChars
@printer Bubble pos:50,50
Lorem ipsum dolor sit amet, consectetur adipiscing elit. Nam porta, turpis id porttitor maximus, mi tellus tempus justo, sit amet porttitor lorem velit vitae libero. Vestibulum tincidunt eget ante eget sodales.
@hidePrinter
@goto .PrinterChoice

# EndPrinter
@char k
@char n
@arrange k.25,n.75
@printer Wide
@stopBgm fade:3
@bgm CloudNine fade:3
k: Okay, and what about making those windows something more than just boring rectangles?
n: Sure, you can modify them any way you want.
@spawn ShakePrinter
k.Surprise: Really?! Wanna try this!
n: Uhm, sure...
@char k.Confidence
@style b
@spawn Blur params:,1,3
@camera offset:-3.75,2 zoom:0.45 time:3
k: .[wait 0.5][spawn ShakePrinter wait!] .[wait 0.5][spawn ShakePrinter wait!] .[spawn ShakePrinter wait!]
@hidePrinter
@style default
@wait 1

@printer CustomPrinter

k.Happy: Ta-daa~!
@despawn Blur params:0.5
@camera offset:0,0 zoom:0
@char n.Surprise look:right pos:50 wait!
n: ...[i] Why cats?
k.Surprise: What, you don't like cats?
@char n look:left
n.Happy: It's complicated...

@choice "Keep the printer" goto:.CustomPrinterKept
@choice "Revert to default" goto:.CustomPrinterChanged
@stop

# CustomPrinterKept
@char n.Default
@char k.Happy
n: Whatever...
@goto .CustomPrinterOut

# CustomPrinterChanged
@printer Wide
@char n.Default
@char k.Angry
@arrange
k: Boring!
@goto .CustomPrinterOut

# CustomPrinterOut
n: Guess we should defuse the situation. How about changing a background?
k.Default: Yeah, I would like some fresh air.
n: Add background assets the same way you've added characters and use <b>@back</b> command to switch them.

@choice "@back Forest" goto:.SelectBack
@stop

# SelectBack
@hidePrinter wait!
@back Forest wait!
@arrange

n: You can also specify a transition effect to use when switching backgrounds. Aside from default cross-fade, there are a total of 23 different transition effects available out of the box and an option to add custom ones.[i] Here are some of them.

# TransitionsChoice
@hidePrinter wait!
@hideChars wait!
@choice "@back Desert.SlideIn" goto:.TransitionsSlideIn
@choice "@back Desert.Crumble" goto:.TransitionsCrumble
@choice "@back Desert.CloudReveal" goto:.TransitionsCloudReveal
@choice "@back Desert.Pixelate" goto:.TransitionsPixelate
@choice "@back Desert.Ripple" goto:.TransitionsRipple
@choice "@back Desert.LineReveal" goto:.TransitionsLineReveal
@choice "@back Desert.RadialBlur" goto:.TransitionsRadialBlur
@choice "@back Desert.Blinds" goto:.TransitionsBlinds
@choice "Ok, I've got it" goto:.TransitionsEnd
@stop

# TransitionsSlideIn
@back Desert.SlideIn time:3 wait!
@wait 1.5
@back Forest.SlideIn time:3 wait!
@goto .TransitionsChoice

# TransitionsCrumble
@back Desert.Crumble time:3 wait!
@wait 1.5
@back Forest.Crumble time:3 wait!
@goto .TransitionsChoice

# TransitionsCloudReveal
@back Desert.CloudReveal time:3 wait!
@wait 1.5
@back Forest.CloudReveal time:3 wait!
@goto .TransitionsChoice

# TransitionsPixelate
@back Desert.Pixelate time:3 wait!
@wait 1.5
@back Forest.Pixelate time:3 wait!
@goto .TransitionsChoice

# TransitionsRipple
@back Desert.Ripple time:3 wait!
@wait 1.5
@back Forest.Ripple time:3 wait!
@goto .TransitionsChoice

# TransitionsLineReveal
@back Desert.LineReveal time:3 wait!
@wait 1.5
@back Forest.LineReveal time:3 wait!
@goto .TransitionsChoice

# TransitionsRadialBlur
@back Desert.RadialBlur time:3 wait!
@wait 1.5
@back Forest.RadialBlur time:3 wait!
@goto .TransitionsChoice

# TransitionsBlinds
@back Desert.Blinds time:3 wait!
@wait 1.5
@back Forest.Blinds time:3 wait!
@goto .TransitionsChoice

# TransitionsEnd
@char k
@char n
@stopBgm fade:10
k: Hm, but all those backgrounds, they look a bit...[wait 0.7] flat?
n: Well, they are sprite backgrounds, after all.[i] Just like characters, backgrounds can be based on multiple implementations: sprites, animated objects, videos and even full-fledged Unity scenes.
k.Surprise: You said videos?
n: Indeed. Simply add a video file as another background resource and use the same command.[i] Check this out.[< skip!]

@choice "@back VideoForest" goto:.SelectVideoBack
@stop

# SelectVideoBack
@hidePrinter wait!
@hideChars wait!
@back VideoForest id:Video time:1.5 wait!
@wait 3

@char n
k.Default: Impressive!
n: Though, I think <b>scene</b> and <b>generic</b> backgrounds are even more amazing. You can put literally anything into them: animated objects, particle systems, even scripted stuff!

k.Angry: Wait, I think we're missing something essential.
n: Like what?
k: Listen closely...
n: ...[i] Don't think I can hear anything.
k.Confidence: Exactly!
n: Ah, I've got it. Right, let's talk about audio.
n: Use <b>@bgm</b> and <b>@sfx</b> commands to play background music and sound effects respectively.[i] Both music and sound effect audio files can be added in the same way as all the other resources via editor GUI.
n: The music will loop by default, though you can change this, as well as volume and fade duration.[i] Here, check out these music tracks.[< skip!]

# BgmChoice
@choice "@bgm CloudNine" goto:.BgmCloudNine
@choice "@bgm JellyRolls" goto:.BgmJellyRolls
@choice "Ok, I've got it!" goto:.EndBgm
@stop

# BgmCloudNine
@hidePrinter
@stopBgm fade:1
@bgm CloudNine fade:1
@wait 3
@goto .BgmChoice

# BgmJellyRolls
@hidePrinter
@stopBgm fade:1
@bgm JellyRolls fade:1
@wait 3
@goto .BgmChoice

# EndBgm
@stopBgm fade:1
n.Default: On the contrary, sound effects won't loop by default.[< skip!]

# SfxChoice
@choice "@sfx Glitch" goto:.SfxGlitch
@choice "@sfx Glitter" goto:.SfxGlitter
@choice "@sfx Bubbles" goto:.SfxBubbles
@choice "Ok, I've got it!" goto:.EndSfx
@stop

# SfxGlitch
@hidePrinter
@sfx Glitch
@wait 0.5
@goto .SfxChoice

# SfxGlitter
@hidePrinter
@sfx Glitter
@wait 0.5
@goto .SfxChoice

# SfxBubbles
@hidePrinter
@sfx Bubbles
@wait 0.5
@goto .SfxChoice

# EndSfx
@hide k
@arrange
n: This concludes the demo.[i] For more information and tutorials check out <b>naninovel.com</b>.
n: See ya!

@title
